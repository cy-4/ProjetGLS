import 'Game.ecore'

package game

context Jeu
inv warningSeverity: false
inv withMessage('Explicit message in game ' + self.name + ' (withMessage)'): false
inv errorSeverity: null

/* Le nom d'un Explorateur doit commencer par une majuscule.
 * Il doit être composé d'au moins deux caracteres. */
context Explorateur
inv validName('Invalid name: ' + self.name):
	self.name.matches('[A-Z][A-Za-z0-9_]+')

/* Taille disponible dans le 'sac' de l'explorateur toujours >=0 */
context Explorateur
inv tailleDispoPos:
	self.tailleDispo >= 0

/* Le nom d'une Personne doit commencer par une majuscule.
 * Il doit etre compose d'au moins deux caracteres. */
context Personne
inv validName('Invalid name: ' + self.name):
	self.name.matches('[A-Z][A-Za-z0-9_]+')
	
/* Deux personnes doivent necessairement avoir deux noms differents */
context Personne
inv uniqueName('Not unique name: ' + self.name):
	Personne.allInstances()->forAll(p1,p2 | p1 <> p2 implies p1.name <> p2.name)

	
/* Deux lieux et/ou chemins doivent necessairement avoir deux noms differents */
context Place
inv uniqueName('Not unique name: ' + self.name):
	Place.allInstances()->forAll(p1,p2 | p1 <> p2 implies p1.name <> p2.name)

/* Il n'y a qu'un Lieu de nature Start */
context Lieu
inv uniqueStart('Not unique start: ' + self.name):
	Lieu.allInstances()
	->select(e|e.nature=Nature::depart)
	->size() = 1

/* Un chemin ne peut pas etre reflexive */
context Chemin
inv CheminNotReflexive:
	self.pred <> self.succ
	
/* Quantite d'Objet positive */
context Objet
inv qtePositive:
	self.qte > 0

/* Taille d'Objet positive */
context Objet
inv taillePositive:
	self.taille > 0
	
/* Une condition commence par une miniscule et a au moins deux caracteres.*/
context Condition
inv validName('Invalid name: ' + self.name):
	self.name.matches('[a-z][A-Za-z0-9_]+')

endpackage
