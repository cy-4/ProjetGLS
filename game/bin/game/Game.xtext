grammar game.Game with org.eclipse.xtext.common.Terminals

generate game "http://www.Game.game"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Jeu: 'jeu' name = ID '{'
	territoire = Territoire
	explorateur = Explorateur
	elementsjeu += ElementsJeu*
'}'
;

enum Nature returns Nature:
	depart = 'depart'
	| fin = 'fin'
	| inter = 'intermediaire'
;

terminal BOOL returns ecore::EBoolean: ('true'|'false');

ElementsJeu: Personne | Avantage | Place;

Place: Lieu | Chemin;

Avantage: Connaissance | Objet;

Territoire: 'territoire' '{'
	places += Place+
'}'
;

Lieu: 'lieu' name = ID ':' nature = Nature;

Chemin: 'chemin' name = ID '{'
	'de' pred = [Lieu]
    'vers' succ = [Lieu]
	'visible' visible = Condition
	'obligatoire' obligatoire = BOOL
	'ouvert' ouvert = BOOL
'}'
;

Personne: 'personne' name = ID '{'
	'localisation' place = [Place]
	'visible' visible = Condition
	'actif' actif = Condition
	'obligatoire' obligatoire = BOOL	
	'interactions' interactions += Interaction*
'}'
;

Explorateur: 'explorateur' '{'
	'localisation' place = [Place]
	'taille' 'disponible' tailleDispo = INT
	('objets' 'possedes' objets += Objet*)?
	('connaissances' 'possedes' connaissances += Connaissance*)?
'}'
;

Connaissance: 'connaissance' name = ID '{'
	'visible' visible = Condition
	'actif' actif = Condition
'}'
;

Objet: 'objet' name = ID '{' 
	'visible' visible = Condition
	'actif' actif = Condition
	'quantite' qte = INT
	'taille' taille = INT 
	'transformable' transformable = BOOL
'}'
;

Interaction : 'interaction' '{'
	'question' question = STRING
	choix += Choix+
'}'
;

Choix: 'choix' numero = INT '{'
	'reponse' reponse = STRING
	(('consomme' | 'donne') qte += INT avantage += [Avantage])*
'}'
;

Condition: ('condition' name = ID '{'
	('connaissances' ('requises' connaissancesRequises += [Connaissance]* | 'interdites' connaissancesInterdites += [Connaissance]*))*
	(quantite = INT 'objets' ('requis' objetsRequis += [Objet]* | 'interdits' objetsInterdits += [Objet]*))*
'}' | BOOL)
;
